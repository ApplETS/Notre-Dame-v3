apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.squareup.sqldelight'
apply from: rootProject.file('gradle/android.gradle')

sqldelight {
    packageName = "ca.etsmtl.applets.shared.db"
    className = "EtsMobileDb"
}

androidExtensions {
    experimental = true
}

kotlin {
    targets {
        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") \
                              ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'iOS') {
            compilations.main.outputKinds('FRAMEWORK')
        }

        fromPreset(presets.android, 'android')
    }

    sourceSets {
        commonMain {
            dependencies {
                api 'org.jetbrains.kotlin:kotlin-stdlib-common'
                api "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$versions.kotlinx_serialization"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$versions.coroutines"
                api "com.soywiz:klock:$versions.klock"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$versions.coroutines"
                implementation "androidx.lifecycle:lifecycle-extensions:$versions.lifecycle"
                implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0-alpha02"
                implementation "io.ktor:ktor-client-core:$versions.ktor"
                implementation "io.ktor:ktor-client-json:$versions.ktor"
            }
        }

        androidMain {
            dependsOn commonMain
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7'
                api "com.squareup.sqldelight:android-driver:$versions.sqldelight"
                api "com.squareup.sqldelight:android-paging-extensions:$versions.sqldelight"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$versions.coroutines"
                implementation "io.ktor:ktor-client-android:$versions.ktor"
                implementation "io.ktor:ktor-client-json-jvm:$versions.ktor"
            }
        }

        iOSMain {
            dependsOn commonMain
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$versions.kotlinx_serialization"
                api "com.squareup.sqldelight:ios-driver:$versions.sqldelight"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$versions.coroutines"
                implementation "io.ktor:ktor-client-ios:$versions.ktor"
                implementation "io.ktor:ktor-client-json-ios:$versions.ktor"
            }
        }
    }
}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

task packForXCode(type: Sync) {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'

    inputs.property "mode", mode
    dependsOn kotlin.targets.iOS.compilations.main.linkTaskName("FRAMEWORK", mode)

    from { kotlin.targets.iOS.compilations.main.getBinary("FRAMEWORK", mode).parentFile }
    into frameworkDir

    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\nexport 'JAVA_HOME=${System.getProperty("java.home")}'\ncd '${rootProject.rootDir}'\n./gradlew \$@\n"
            setExecutable(true)
        }
    }
}

tasks.build.dependsOn packForXCode